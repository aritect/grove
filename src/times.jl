"""RFC3339 UTC date-time second precision, e.g. `2026-05-05T14:03:22Z`."""
function utc_stamp_second()::String
    Dates.format(Dates.now(Dates.UTC), dateformat"yyyy-mm-ddTHH:MM:SS") * "Z"
end

_timestamp_blank(x)::Bool = isempty(x) || isempty(strip(String(x)))

function migrate_missing_timestamps_nodes!(st::State)::Nothing
    for (_, n) in st.nodes
        tc = get(n.attrs, "t_created", "")
        tu = get(n.attrs, "t_updated", "")
        mc = _timestamp_blank(tc)
        mu = _timestamp_blank(tu)
        if mc && mu
            t = utc_stamp_second()
            n.attrs["t_created"] = t
            n.attrs["t_updated"] = t
        elseif mc
            n.attrs["t_created"] = strip(String(tu))
        elseif mu
            n.attrs["t_updated"] = strip(String(tc))
        end
    end
    nothing
end

function migrate_missing_timestamps_edges!(st::State)::Nothing
    for e in st.edges
        if e.t_created === nothing || _timestamp_blank(e.t_created)
            e.t_created = utc_stamp_second()
        end
    end
    nothing
end

function stamp_new_node!(n::Node)::Nothing
    t = utc_stamp_second()
    n.attrs["t_created"] = t
    n.attrs["t_updated"] = t
    nothing
end

function stamp_touch_node!(n::Node)::Nothing
    n.attrs["t_updated"] = utc_stamp_second()
    if _timestamp_blank(get(n.attrs, "t_created", ""))
        n.attrs["t_created"] = n.attrs["t_updated"]
    end
    nothing
end

function stamp_new_edge!(e::Edge)::Nothing
    if e.t_created === nothing || _timestamp_blank(e.t_created)
        e.t_created = utc_stamp_second()
    end
    nothing
end
